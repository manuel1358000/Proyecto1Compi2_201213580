package Analizadores.GramaticaFS;
import java_cup.runtime.Symbol;
import java_cup.runtime.*;
import java.lang.*;
import javax.swing.JOptionPane;
import java.io.*;
import java.util.LinkedList;
import java.awt.Color;
import ArbolAST.AST;
import ArbolAST.NodoAST;
import ArbolAST.Entorno.Type;
import ArbolAST.Instrucciones.Declaracion;
import ArbolAST.Expresiones.Expresion;
import ArbolAST.Expresiones.operacion.Logica;
import ArbolAST.Expresiones.operacion.Aritmetica;
import ArbolAST.Expresiones.operacion.Relacional;
import ArbolAST.Expresiones.operacion.Operacion.Operador;
import ArbolAST.Expresiones.operacion.Ternario;
import ArbolAST.Instrucciones.Imprimir;
import ArbolAST.Instrucciones.Detener;
import ArbolAST.Instrucciones.Seleccion.If;
import ArbolAST.Instrucciones.Seleccion.SubIf;
import ArbolAST.Instrucciones.Seleccion.Caso;
import ArbolAST.Expresiones.operacion.Retornar;
import ArbolAST.Instrucciones.Seleccion.Switch;
parser code
{:
    public AST root;
    public static int index=0;
    public void syntax_error(Symbol s){
        System.out.println("Error sintactico en la linea "+(s.left+1)+" Columna "+s.right+". Identificador "+s.value+" no reconocido"); 
    }
    /**Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores.*/
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        javax.swing.JOptionPane.showMessageDialog(null,"Error Sintactico en la linea "+(s.right+1)+"Columna "+s.left+". Identificador"+s.value+" no reconocido.");
    }
:}
action code
{:  
:}

//DEFINICION DE SIMBOLOS DEL LENGUAJE FS
terminal String parena,parenc,llavea,llavec,igual,mas,menos,por,div,pot,mayorq,menorq,mayorigualq,menorigualq,igualigual,diferente,and,or,not;
terminal String coma,pyc,corchea,corchec,punto,dospuntos,signoi;
//DEFINICION DE PALABRAS RESERVADAS
terminal String var,descendente,ascendente,invertir,maximo,minimo,filtrar;
terminal String buscar,map,reduce,todos,alguno,imprimir,importar,si,sino,caso,detener,retornar,selecciona;
terminal String defecto;

//DEFINICION VALORES IMPLICITOS LENGUAJE FS
terminal String valor_id,valor_numero_completo,valor_path,valor_path2,valor_cadena,valor_verdadero,valor_falso,valor_simbolo;
terminal String valor_numero_decimal,valor_nulo;

//definicion de no terminales
non terminal AST INICIAL;
non terminal LinkedList<NodoAST> SENTENCIAS_GLOBALES;
non terminal LinkedList<NodoAST> SENTENCIA_GLOBAL;
non terminal LinkedList<NodoAST> DECLARACION_VARIABLES;
non terminal LinkedList<NodoAST> LISTA_ID;
non terminal Expresion EXPRESION;
non terminal LinkedList<NodoAST> LISTA_PARAMETROS;
non terminal NodoAST LLAMADA_FUNCION;
non terminal LinkedList<NodoAST> SENTENCIAS_GENERALES;
non terminal LinkedList<NodoAST> SENTENCIA_IMPRIMIR;
non terminal LinkedList<NodoAST> SENTENCIAS_SELECCION;
non terminal Expresion TERNARIO;
non terminal NodoAST SENTENCIA_SI;
non terminal LinkedList<SubIf> SENTENCIA_SINOSI;
non terminal SubIf SENTENCIA_SINO;
non terminal NodoAST SENTENCIA_SELECCIONA;
non terminal LinkedList<Caso> LISTAS_CASOS;
non terminal Caso LISTA_CASO;
non terminal Caso CASO_DEFECTO;
non terminal LinkedList<NodoAST> SENTENCIA_INTERNA;




non terminal LinkedList<NodoAST> SENTENCIA_RETORNAR;
non terminal LinkedList<NodoAST> SENTENCIA_DETENER;



non terminal String ELEMENTOS_ARRAY,LISTA_ARRAY,LISTA_PARAMETRO,FUNCIONES_LENGUAJE;
non terminal String ELEMENTOS_OBJETO,ELEMENTO_PATH;



non terminal LinkedList<NodoAST> SENTENCIAS_INTERNAS;
//DEFINICION DE LA PRECEDENCIA DE LOS OPERADORES
precedence left signoi;
precedence left or;
precedence left and;
precedence right not;
precedence left mayorq,menorq,mayorigualq, menorigualq,igualigual,diferente;

precedence left mas,menos;
precedence left div, por;
precedence left pot;
precedence left parena, parenc;

start with INICIAL;
INICIAL::= SENTENCIAS_GLOBALES:a{:
                                this.parser.root=new AST(a);
                               :};
SENTENCIAS_GLOBALES::= SENTENCIAS_GLOBALES:a SENTENCIA_GLOBAL:b{:
                                                                RESULT=a;
                                                                RESULT.addAll(b);
                                                               :}
                     | SENTENCIA_GLOBAL:a{:
                                            RESULT=a;
                                         :};

//DECLARACION DE VARIABLES,FUNCIONES, METODOS,OBJETOS,ARREGLOS

SENTENCIA_GLOBAL::= SENTENCIAS_GENERALES:a{:RESULT=a;:}
                  | importar parena ELEMENTO_PATH parenc pyc;

SENTENCIAS_INTERNAS::= SENTENCIAS_INTERNAS:a SENTENCIAS_GENERALES:b{:
                                                                    RESULT=a;
                                                                    RESULT.addAll(b);
                                                                   :}
                     | SENTENCIAS_GENERALES:a{:
                                           RESULT=a;
                                          :};

SENTENCIAS_GENERALES::= SENTENCIA_IMPRIMIR:a{:
                                            RESULT=a;
                                            :}
                      | DECLARACION_VARIABLES:a{:
                                                RESULT=a;
                                               :} 
                      | SENTENCIAS_SELECCION:a{:RESULT=a;:}
                      | SENTENCIA_RETORNAR:a{:RESULT=a;:}
                      | SENTENCIA_DETENER:a{:RESULT=a;:};

SENTENCIA_RETORNAR::= retornar EXPRESION:a pyc{:
                                                RESULT=new LinkedList<>();
                                                RESULT.add(new Retornar(a));
                                              :};
SENTENCIA_DETENER::= detener pyc{:
                                    RESULT=new LinkedList<>();
                                    RESULT.add(new Detener());
                                :};

SENTENCIA_IMPRIMIR::=imprimir:a parena EXPRESION:b parenc pyc{:
                                                                RESULT=new LinkedList<NodoAST>();
                                                                RESULT.add(new Imprimir(b));
                                                             :};
SENTENCIAS_SELECCION::= SENTENCIA_SI:a{:
                                        RESULT=new LinkedList<NodoAST>();
                                        RESULT.add(a);
                                      :}
                      | SENTENCIA_SELECCIONA:a{:
                                                RESULT=new LinkedList<NodoAST>();
                                                RESULT.add(a);
                                              :};


SENTENCIA_SELECCIONA::= selecciona parena EXPRESION:a parenc llavea LISTAS_CASOS:b CASO_DEFECTO:c llavec{:
                                                                                                        RESULT=new Switch(a,b,c);
                                                                                                        :}
                      | selecciona parena EXPRESION:a parenc llavea LISTAS_CASOS:b llavec{:
                                                                                            RESULT=new Switch(a,b,null);
                                                                                         :};

LISTAS_CASOS::= LISTAS_CASOS:a LISTA_CASO:b{:
                                            RESULT=a;
                                            RESULT.add(b);
                                           :}
              | LISTA_CASO:a{:
                                RESULT=new LinkedList<>();
                                RESULT.add(a);
                            :};

LISTA_CASO::= caso EXPRESION:a dospuntos llavea SENTENCIAS_INTERNAS:b llavec{:
                                                                             RESULT=new Caso(a,b);
                                                                            :};

CASO_DEFECTO::= defecto dospuntos llavea SENTENCIAS_INTERNAS:a llavec{:
                                                                    RESULT=new Caso(a);
                                                                   :};



SENTENCIA_SI::= si parena EXPRESION:a parenc llavea SENTENCIAS_INTERNAS:b llavec SENTENCIA_SINOSI:c SENTENCIA_SINO:d{:
                                                                                                                        RESULT=new If(a,b,c,d);
                                                                                                                    :}
              | si parena EXPRESION:a parenc llavea SENTENCIAS_INTERNAS:b llavec SENTENCIA_SINOSI:c{:
                                                                                                    RESULT=new If(a,b,c,new SubIf());
                                                                                                   :}
              | si parena EXPRESION:a parenc llavea SENTENCIAS_INTERNAS:b llavec SENTENCIA_SINO:c{:
                                                                                                    RESULT=new If(a,b,new LinkedList<>(),c);
                                                                                                 :}
              | si parena EXPRESION:a parenc llavea SENTENCIAS_INTERNAS:b llavec{:
                                                                                 RESULT=new If(a,b,new LinkedList<>(),new SubIf());
                                                                                :};



SENTENCIA_SINO::= sino llavea SENTENCIAS_INTERNAS:a llavec{:
                                                            RESULT=new SubIf(null,a,true);
                                                          :};

SENTENCIA_SINOSI::= SENTENCIA_SINOSI:a sino si parena EXPRESION:b parenc llavea SENTENCIAS_INTERNAS:c llavec{:
                                                                                                                RESULT=a;
                                                                                                                RESULT.add(new SubIf(b,c,false));
                                                                                                            :}
                  | sino si parena EXPRESION:a parenc llavea SENTENCIAS_INTERNAS:b llavec{:
                                                                                            RESULT=new LinkedList<SubIf>();
                                                                                            RESULT.add(new SubIf(a,b,false));
                                                                                         :};


DECLARACION_VARIABLES::= var LISTA_ID:a pyc{:
                                            RESULT=a;
                                           :}
                       | var LISTA_ID:a igual EXPRESION:b pyc{:
                                                                int indice=a.size()-1;
                                                                Declaracion ultima=(Declaracion)a.get(indice);
                                                                a.remove(indice);
                                                                ultima.setIntValue(b);
                                                                a.add(ultima);
                                                                RESULT=a;
                                                             :}
                       | var LISTA_ID igual corchea ELEMENTOS_ARRAY corchec pyc
                       | var LISTA_ID igual llavea ELEMENTOS_OBJETO llavec pyc;

ELEMENTO_PATH::= valor_path
                | valor_path2;

ELEMENTOS_OBJETO::= ELEMENTOS_OBJETO coma valor_id dospuntos EXPRESION
                  | valor_id dospuntos EXPRESION;

ELEMENTOS_ARRAY::=  
                 | LISTA_ARRAY;
LISTA_ARRAY::= LISTA_ARRAY coma EXPRESION
             | EXPRESION;
LISTA_ID::= LISTA_ID:a coma valor_id:b{:
                                       RESULT=a;
                                       Declaracion declaracion=new Declaracion(Type.PrimitiveType.NULL,b.toString(),null,bleft);
                                       RESULT.add(declaracion);
                                      :}
          | valor_id:a{:
                        RESULT=new LinkedList<NodoAST>();
                        Declaracion declaracion=new Declaracion(Type.PrimitiveType.NULL,a.toString(),null,aleft);
                        RESULT.add(declaracion);
                      :};


EXPRESION::= menos EXPRESION:a{:
                                            RESULT=new Aritmetica(a,true,Operador.UNARIO);
                                       :}
           | EXPRESION:a mas EXPRESION:b{:
                                            RESULT=new Aritmetica(a,b,Operador.SUMA);
                                       :}
           | EXPRESION:a menos EXPRESION:b{:
                                            RESULT=new Aritmetica(a,b,Operador.RESTA);
                                       :}
           | EXPRESION:a por EXPRESION:b{:
                                            RESULT=new Aritmetica(a,b,Operador.MULTIPLICACION);
                                       :}
           | EXPRESION:a div EXPRESION:b{:
                                            RESULT=new Aritmetica(a,b,Operador.DIVISION);
                                       :}
           | EXPRESION:a pot EXPRESION:b{:
                                            RESULT=new Aritmetica(a,b,Operador.POTENCIA);
                                       :}
           | EXPRESION:a mayorq EXPRESION:b{:
                                            RESULT=new Relacional(a,b,Operador.MAYORQ);
                                       :}
           | EXPRESION:a menorq EXPRESION:b{:
                                            RESULT=new Relacional(a,b,Operador.MENORQ);
                                       :}
           | EXPRESION:a mayorigualq EXPRESION:b{:
                                            RESULT=new Relacional(a,b,Operador.MAYORIGUALQ);
                                       :}
           | EXPRESION:a menorigualq EXPRESION:b{:
                                            RESULT=new Relacional(a,b,Operador.MENORIGUALQ);
                                       :}
           | EXPRESION:a igualigual EXPRESION:b{:
                                            RESULT=new Relacional(a,b,Operador.IGUALIGUAL);
                                       :}
           | EXPRESION:a diferente EXPRESION:b{:
                                            RESULT=new Relacional(a,b,Operador.DIFERENTE);
                                       :}
           | EXPRESION:a and EXPRESION:b{:
                                            RESULT=new Logica(a,b,Operador.AND);
                                       :}
           | EXPRESION:a or EXPRESION:b{:
                                            RESULT=new Logica(a,b,Operador.OR);
                                       :}
           | not EXPRESION:a{:
                                RESULT=new Logica(a,true,Operador.NOT);
                            :}
           | parena EXPRESION:a parenc{:
                                        RESULT=a;
                                      :}
           | valor_id:a{:
                        RESULT=new Aritmetica(a.toString(),Type.PrimitiveType.ID);
                       :}
           | valor_numero_completo:a{:
                                        RESULT=new Aritmetica(a.toString(),Type.PrimitiveType.INTEGER);
                                    :}
           | valor_numero_decimal:a{:
                                    RESULT=new Aritmetica(a.toString(),Type.PrimitiveType.DOUBLE);
                                   :}
           | valor_cadena:a{:
                            RESULT=new Aritmetica(a.toString(),Type.PrimitiveType.STRING);
                           :}
           | valor_verdadero:a{:
                                RESULT=new Aritmetica(a.toString(),Type.PrimitiveType.BOOLEAN);
                              :}
           | valor_falso:a{:
                            RESULT=new Aritmetica(a.toString(),Type.PrimitiveType.BOOLEAN);
                          :}
           | valor_id punto FUNCIONES_LENGUAJE
           | valor_id corchea EXPRESION corchec
           | LLAMADA_FUNCION
           | TERNARIO:a{:RESULT=a;:};

TERNARIO::=EXPRESION:a signoi EXPRESION:b dospuntos EXPRESION:c{:RESULT=new Ternario(a,b,c);:};

FUNCIONES_LENGUAJE::= descendente parena parenc
                    | ascendente parena parenc
                    | invertir parena parenc
                    | maximo parena parenc
                    | minimo parena parenc
                    | filtrar parena LISTA_PARAMETROS parenc
                    | buscar parena LISTA_PARAMETRO parenc
                    | map parena LISTA_PARAMETRO parenc
                    | reduce parena LISTA_PARAMETRO parenc
                    | todos parena LISTA_PARAMETRO parenc
                    | alguno parena LISTA_PARAMETRO parenc;


LLAMADA_FUNCION::= valor_id parena LISTA_PARAMETROS parenc;

LISTA_PARAMETROS::=
                  | LISTA_PARAMETRO;
LISTA_PARAMETRO::= LISTA_PARAMETRO coma EXPRESION
                  | EXPRESION;